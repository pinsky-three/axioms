WHITESPACE = _{ " " | "\t" | "\r\n" | "\n" }
COMMENT    = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

ident = @{ ASCII_ALPHA+ }

number = {
    ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)?
  | "." ~ ASCII_DIGIT+
}

imag_literal = {
    (ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)?) ~ "i"
  | ("." ~ ASCII_DIGIT+) ~ "i"
}

program = { statement ~ (";" ~ statement)* ~ ";"? }

statement = { function_definition | expression }

function_definition = { ident ~ "(" ~ parameter_list? ~ ")" ~ "=" ~ expression }
parameter_list      = { ident ~ ("," ~ ident)* }

expression = { sum }

sum    = { product ~ (add_op ~ product)* }
add_op = { "+" | "-" }

product      = { implicit_mul }
implicit_mul = { power ~ (mul_op ~ power | &(ident | "(" | number | imag_literal) ~ power)* }
mul_op       = { "*" | "/" }

power  = { unary ~ (exp_op ~ unary)* }
exp_op = { "^" }

unary    = { unary_op* ~ primary }
unary_op = { "-" | "+" }

primary = {
    imag_literal
  | number
  | function_call
  | "(" ~ expression ~ ")"
  | ident
}

function_call = { ident ~ "(" ~ (expression ~ ("," ~ expression)*)? ~ ")" }
